buildscript {
    ext {
        springBootVersion = '1.3.5.RELEASE'
    }
    repositories {
        mavenCentral()
    }
    dependencies {
        classpath("org.springframework.boot:spring-boot-gradle-plugin:${springBootVersion}")
    }
}

apply plugin: 'java'
apply plugin: 'spring-boot'

jar {
    baseName = 'custom-repackage-test'
    version = '0.0.1-SNAPSHOT'
}
sourceCompatibility = 1.8
targetCompatibility = 1.8

repositories {
    mavenCentral()
}

sourceSets {
    integTest {
        java {
            compileClasspath += main.output + test.output
            runtimeClasspath += main.output + test.output
            srcDir file("src/integ-test/java")
        }
        resources.srcDir file("src/integ-test/resources")
    }
}

configurations {
    integTestCompile.extendsFrom testCompile
    integTestRuntime.extendsFrom testRuntime
}

dependencies {
    compile('org.springframework.boot:spring-boot-starter')
    testCompile('org.springframework.boot:spring-boot-starter-test')
}

task integTest(type: Test) {
    testClassesDir = sourceSets.integTest.output.classesDir
    classpath = sourceSets.integTest.runtimeClasspath
    outputs.upToDateWhen { false }
}

// These lines fix the issue
//bootRepackage {
//    mainClass = 'org.springframework.boot.issues.gh5956.Application'
//}
//
//ext.mainClassName = "org.springframework.boot.issues.gh5956.IntegrationTestSuite"


task integTestJar(type: Jar) {
    classifier = 'tests'
    from sourceSets.integTest.output + sourceSets.main.output + sourceSets.test.output
}

task integTestBoot(type: BootRepackage, dependsOn: integTestJar) {
    withJarTask = integTestJar
    mainClass = 'org.springframework.boot.issues.gh5956.IntegrationTestSuite'
    customConfiguration = "integTestRuntime"
}

